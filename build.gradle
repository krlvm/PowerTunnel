plugins {
    id 'java'
    id 'application'
    id 'distribution'
}

group 'io.github.krlvm.powertunnel'
version '1.15-SNAPSHOT'
ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

application {
    mainClass = 'ru.krlvm.powertunnel.PowerTunnel'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'xyz.rogfam:littleproxy:2.0.5'
    implementation 'org.slf4j:slf4j-simple:1.6.1'
    implementation 'net.java.dev.jna:jna:3.2.5'

    /* dnsjava */
    implementation 'dnsjava:dnsjava:3.4.0'
    implementation 'org.jitsi:dnssecjava:2.0.0'

    /* LittleProxy-MITM Dependencies */
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.65'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

jar {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
            'Main-Class': application.mainClass
        )
    }
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

run {
    File wd = new File('build/run/')
    wd.mkdirs()
    workingDir = wd
}